<!DOCTYPE html>
<script src='src/third-party/webvr-polyfill.js'></script>
<script src="src/third-party/WebVR.js"></script>
<script src="src/third-party/three.js"></script>
<script src="src/third-party/FlyControls.js"></script>

<script id="fragSky" type="x-shader/x-fragment">

  // Cellular noise ("Worley noise") in 2D in GLSL.
  // Copyright (c) Stefan Gustavson 2011-04-19. All rights reserved.
  // This code is released under the conditions of the MIT license.
  // See LICENSE file for details.
  // https://github.com/stegu/webgl-noise
  
  // Modulo 289 without a division (only multiplications)
  vec2 mod289(vec2 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  // Modulo 7 without a division
  vec4 mod7(vec4 x) {
    return x - floor(x * (1.0 / 7.0)) * 7.0;
  }
  
  // Permutation polynomial: (34x^2 + x) mod 289
  vec4 permute(vec4 x) {
    return mod289((34.0 * x + 1.0) * x);
  }
  
  // Cellular noise, returning F1 and F2 in a vec2.
  // Speeded up by using 2x2 search window instead of 3x3,
  // at the expense of some strong pattern artifacts.
  // F2 is often wrong and has sharp discontinuities.
  // If you need a smooth F2, use the slower 3x3 version.
  // F1 is sometimes wrong, too, but OK for most purposes.
  vec2 cellular2x2(vec2 P) {
  #define K 0.142857142857 // 1/7
  #define K2 0.0714285714285 // K/2
  #define jitter 0.8 // jitter 1.0 makes F1 wrong more often
    vec2 Pi = mod289(floor(P));
     vec2 Pf = fract(P);
    vec4 Pfx = Pf.x + vec4(-0.5, -1.5, -0.5, -1.5);
    vec4 Pfy = Pf.y + vec4(-0.5, -0.5, -1.5, -1.5);
    vec4 p = permute(Pi.x + vec4(0.0, 1.0, 0.0, 1.0));
    p = permute(p + Pi.y + vec4(0.0, 0.0, 1.0, 1.0));
    vec4 ox = mod7(p)*K+K2;
    vec4 oy = mod7(floor(p*K))*K+K2;
    vec4 dx = Pfx + jitter*ox;
    vec4 dy = Pfy + jitter*oy;
    vec4 d = dx * dx + dy * dy; // d11, d12, d21 and d22, squared
    // Sort out the two smallest distances
  #if 0
    // Cheat and pick only F1
    d.xy = min(d.xy, d.zw);
    d.x = min(d.x, d.y);
    return vec2(sqrt(d.x)); // F1 duplicated, F2 not computed
  #else
    // Do it right and find both F1 and F2
    d.xy = (d.x < d.y) ? d.xy : d.yx; // Swap if smaller
    d.xz = (d.x < d.z) ? d.xz : d.zx;
    d.xw = (d.x < d.w) ? d.xw : d.wx;
    d.y = min(d.y, d.z);
    d.y = min(d.y, d.w);
    return sqrt(d.xy);
  #endif
  }
  
  varying vec2 vUv;
  void main(void) {

    // Stars
    vec2 st = gl_FragCoord.xy/vec2(128,128);
    st *= 10.;
    vec2 F = cellular2x2(st);
    float n = smoothstep(0.03, 0.012, F.x);
    vec2 p = gl_FragCoord.xy;
    vec4 stars = vec4(n, n, n, 1.0);

    // Horizon
    vec4 horizonColor = vec4(173.0/255.0, 220.0/255.0, 255.0/255.0, 1.0);
    float horizonEndY = 0.7;
    float horizon = smoothstep(0.0, horizonEndY, vUv.y);

    vec4 res = mix(horizonColor, stars, horizon);

    gl_FragColor = res;
  }

</script>

<script id="vertSky" type="x-shader/x-vertex">
  varying vec2 vUv;
  void main( void ) {
    vUv = uv;
    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
  }

</script>

<script id="fragLava" type="x-shader/x-fragment">

  varying vec2 vUv;
  varying float noise;
  uniform sampler2D texture;

  void main() {
    vec2 tPos = vec2( 0, clamp(noise, 0.12, 0.5));
    vec4 color = texture2D( texture, tPos );

    if(distance(vUv,vec2(0.5)) < 0.04) {
      color = color * vec4( 0.7, 0.7, 0.7, 1.0);
    }
    gl_FragColor = vec4( color.rgb, 1.0 );
  }

</script>
  
<script id="vertLava" type="x-shader/x-vertex">

  // Description : Array and textureless GLSL 2D/3D/4D simplex 
  //               noise functions.
  //      Author : Ian McEwan, Ashima Arts.
  //  Maintainer : stegu
  //     Lastmod : 20110822 (ijm)
  //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
  //               Distributed under the MIT License. See LICENSE file.
  //               https://github.com/ashima/webgl-noise
  //               https://github.com/stegu/webgl-noise
  // 

  vec3 mod289(vec3 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }

  vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }

  vec4 permute(vec4 x) {
    return mod289(((x*34.0)+1.0)*x);
  }

  vec4 taylorInvSqrt(vec4 r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  float snoise(vec3 v) {  
    const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
    const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

    // First corner
    vec3 i  = floor(v + dot(v, C.yyy) );
    vec3 x0 =   v - i + dot(i, C.xxx) ;

    // Other corners
    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min( g.xyz, l.zxy );
    vec3 i2 = max( g.xyz, l.zxy );

    //   x0 = x0 - 0.0 + 0.0 * C.xxx;
    //   x1 = x0 - i1  + 1.0 * C.xxx;
    //   x2 = x0 - i2  + 2.0 * C.xxx;
    //   x3 = x0 - 1.0 + 3.0 * C.xxx;
    vec3 x1 = x0 - i1 + C.xxx;
    vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
    vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

    // Permutations
    i = mod289(i); 
    vec4 p = permute( permute( permute( 
              i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
            + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
            + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

    // Gradients: 7x7 points over a square, mapped onto an octahedron.
    // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
    float n_ = 0.142857142857; // 1.0/7.0
    vec3  ns = n_ * D.wyz - D.xzx;

    vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

    vec4 x = x_ *ns.x + ns.yyyy;
    vec4 y = y_ *ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);

    vec4 b0 = vec4( x.xy, y.xy );
    vec4 b1 = vec4( x.zw, y.zw );

    //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
    //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
    vec4 s0 = floor(b0)*2.0 + 1.0;
    vec4 s1 = floor(b1)*2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));

    vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
    vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

    vec3 p0 = vec3(a0.xy,h.x);
    vec3 p1 = vec3(a0.zw,h.y);
    vec3 p2 = vec3(a1.xy,h.z);
    vec3 p3 = vec3(a1.zw,h.w);

    //Normalise gradients
    vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;

    // Mix final noise value
    vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
    m = m * m;
    return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                  dot(p2,x2), dot(p3,x3) ) );
  }

  float lava( vec3 coord ) {

    float n = 0.0;
    n += 0.6 * abs( snoise( coord * 3.0 ) );
    return n;

  }

  varying float noise;
  varying vec2 vUv;
  uniform vec2 offset;
  uniform float time;
  void main( void ) {
    vUv = uv;

    noise = lava( vec3( uv*3.0 + offset, time*0.05 ) );

    vec3 newPosition = position + ( normal*200.0*noise );
    gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );

  }

</script>


<script id="fragmentShaderNoise" type="x-shader/x-fragment">

  varying float noise;
  varying vec2 vUv;
  uniform sampler2D texture;

  void main() {
      vec2 tPos = vec2( 0, noise);
      vec4 color = texture2D( texture, tPos );
      if(distance( vUv, vec2(0.5)) < 0.04) {
        color = color * vec4( 0.7, 0.7, 0.7, 1.0);
      }
      gl_FragColor = vec4( color.rgb, 1.0 );
  }

</script>

<script id="vertexShader" type="x-shader/x-vertex">

  //
// Description : Array and textureless GLSL 2D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//               https://github.com/stegu/webgl-noise
// 

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v)
  {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

  float terrain( vec2 coord ) {

    float n = 0.0;

    n += 1.2 * abs( snoise( coord ) );
    n += 0.8 * abs( snoise( coord * 2.0 ) );
    n += 0.1 * abs( snoise( coord * 8.0 ) );

    return n;

  }

  varying float noise;
  uniform float scale;
  uniform vec2 offset;
  varying vec2 vUv;
  void main( void ) {

    vUv = uv;

    noise = terrain( uv * 3.0 + offset );
    vec3 newPosition = position + (normal*scale*noise);
    gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );

  }

</script>

<style>
  .container {
    position: relative;
    text-align: center;
    color: white;
  }

.centered {
	position: absolute;
  top: 10%;
  left: 50%;
  transform: translate(-50%, -50%);
  font-size: 42px;
  font-weight: bold
}
.centered2 {
	position: absolute;
  top: 14%;
  left: 50%;
  transform: translate(-50%, -50%);
  font-size: 20px;
}
</style>

<html>

<head>
    <meta charset="utf-8">
    <title>Exploring lava planet</title>
</head>

<body>
  <section id="appContainer" class="container">
    <div class="centered">EXPLORING LAVA PLANET</div>
    <div class="centered2">
      <b>Point with mouse:</b> move <b style="margin-left: 30px">Q|E:</b> roll
      </div>
  </section>
</body>

</html>
